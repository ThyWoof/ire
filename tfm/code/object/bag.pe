##
##	Container stuff
##


##
##	Open a chest
##

function opencontainer

string str

# Get the name of the object it turns into from the OpenBag table
get_data str where openbag = current.name

# If we got something, make the change
if err = 0
	change current = str
	reset_flag current IS_CONTAINER
	object_sound "open_door" current
endif
end

##
##	Close a chest
##

function closecontainer

string str

# Get the name of the object it turns into from the CloseBag table
get_data str where closebag = current.name

# If we got something, make the change
if err = 0
	change current = str
	reset_flag current IS_CONTAINER
	object_sound "open_door" current
endif
end

##
##	Smash a chest
##

function smashchest

# Spill the goodies
spill current
object_sound "CHAIRBREAKING" current
change current = "chestbroken"

end


##
##	Spill
##


function spill
spill current
end


##
##	Use KFS bag
##

function use_kfs_bag
object kfs
let kfs = current

print "Use bag on "
redraw_text

call get_near
if_not_exists current
	print "nothing."
	printcr
	return
endif

print current.shortdesc
printcr

let victim = current
let suspect = player

if current is called "TREASURE_06"
	object_sound "money_stretched" me
	if current.funcs.gcache > 0
		call current.funcs.get
	endif
	call justice_theft

	give 500000 "gold_coins" to kfs
	destroy current
	return
endif

if current = kfs
	lightning 50
	play_sound "beer_brk"
	let player.stats.hp = -100000
	check_hurt player
	set_flag kfs IS_INVISIBLE = 1

	let kfs.user.user18 = 48
	let kfs.user.user17 = 2
	startfx kfs does fx_blackcorona
	

	wait 2000 NONBLOCKING

	# End the world, even if the player used a patsy
	restart_game
	return
endif

if_flag current IS_LARGE
	print msg_WontFit
	printcr
	return
endif

if_flag current IS_FIXED
	print msg_WontFit
	printcr
	return
endif

if_flag current IS_PERSON
	print msg_CannotCarryThem
	printcr
	return
endif

if current.funcs.gcache > 0
	call current.funcs.get
endif
call justice_theft

move_to_pocket current to kfs

end
